#
# A theme based on steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>

function prompt_theron_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

# Show Git branch/tag, or name-rev if on detached head
function parse_git_branch() {
  (git rev-parse --abbrev-ref HEAD) 2> /dev/null || echo "%F{white}none"
}

function prompt_theron_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_theron_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_THERON_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_THERON_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_THERON_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:dirty' format "%F{red}*%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' 'on %b%D '

  git_branch_info() {echo "${__PROMPT_THERON_COLORS[1]}$(parse_git_branch)%f"}

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '%v'

  PROMPT="%F{cyan}%n%f at %F{166}%m%f in %U${__PROMPT_THERON_COLORS[5]}%c%f%u "'on $(git_branch_info) '${__PROMPT_THERON_COLORS[1]}"%fusing "'%F{blue}$(rvm current)'"
%F{green}Î» %f"
}

prompt_theron_setup "$@"
